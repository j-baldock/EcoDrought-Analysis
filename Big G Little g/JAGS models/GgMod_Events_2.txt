model {

##--- LIKELIHOOD ---------------------------------------------------##

for (i in 1:nObs) {
  Qg[i] ~ dnorm(mu[i], pow(sigma[i], -2))
  mu[i] <- alpha[sites[i]] + beta[sites[i]] * QG[i]
  
  # effect of Big G yield on global process error
  log(sigma[i]) <- sig.alpha[sites[i]] + sig.beta[sites[i]] * QG[i]
  
  # Log-likelihood
  loglik[i] <- logdensity.norm(Qg[i], mu[i], pow(sigma[i], -2))
  }


##--- PRIORS --------------------------------------------------------##

# Site-specific parameters
for (j in 1:nSites) {
    alpha[j] ~ dnorm(alpha.mu, pow(alpha.sigma, -2))
    beta[j] ~ dnorm(beta.mu, pow(beta.sigma, -2))
    sig.alpha[j] ~ dnorm(sig.alpha.mu, pow(sig.alpha.sigma, -2))
    sig.beta[j] ~ dnorm(sig.beta.mu, pow(sig.beta.sigma, -2))
    }
    
# global intercept and slope
alpha.mu ~ dnorm(0, pow(10, -2))
beta.mu ~ dnorm(0, pow(10, -2))

# global process error
sig.alpha.mu ~ dnorm(0, pow(10, -2))
sig.beta.mu ~ dnorm(0, pow(10, -2))

# among-site variation in intercept, slope, and process errors
alpha.sigma ~ dunif(0.001, 100)
beta.sigma ~ dunif(0.001, 100)
sig.alpha.sigma ~ dunif(0.001, 100)
sig.beta.sigma ~ dunif(0.001, 100)

    
##--- DERIVED VALUES ------------------------------------------------##

# expected deviation from Big G
for (j in 1:nSites) { 
  for (k in 1:nDiff) {
    predlg[j,k] <- alpha[j] + beta[j] * QGvec[k]
    diff[j,k] <- (alpha[j] + beta[j] * QGvec[k]) - QGvec[k]
  }}


}